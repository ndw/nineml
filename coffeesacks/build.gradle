buildscript {
  repositories {
    mavenLocal()
    mavenCentral()
    maven { url "https://dev.saxonica.com/maven" }
  }

  configurations.all {
    resolutionStrategy {
      force 'xml-apis:xml-apis:1.4.01',
        "${saxonGroup}:${saxonEdition}:${saxonVersion}"
    }
  }

  dependencies {
    classpath group: saxonGroup, name: saxonEdition, version: saxonVersion
    classpath group: 'org.docbook', name: 'schemas-docbook', version: docbookVersion
    classpath group: 'org.docbook', name: 'docbook-xslTNG', version: xslTNGversion
  }
}

plugins {
  id "java-library"
  id "maven-publish"
  id "signing"
  id 'com.github.gmazzo.buildconfig' version "4.1.2"
  id 'com.nwalsh.gradle.saxon.saxon-gradle' version '0.10.5'
  id 'com.nwalsh.gradle.relaxng.validate' version '0.10.3'
  id 'com.nwalsh.gradle.docker.container' version '0.0.5'
}

import com.nwalsh.gradle.saxon.SaxonXsltTask
import com.nwalsh.gradle.relaxng.validate.RelaxNGValidateTask
import com.nwalsh.gradle.docker.DockerContainer
import net.sf.saxon.s9api.Processor
import java.nio.file.Paths
import java.nio.file.Files

repositories {
  mavenLocal()
  mavenCentral()
  maven { url "https://dev.saxonica.com/maven" }
}

configurations.all {
  resolutionStrategy {
    force 'xml-apis:xml-apis:1.4.01',
      "${saxonGroup}:${saxonEdition}:${saxonVersion}"
  }
}

configurations {
  transform.extendsFrom documentation
}

dependencies {
  implementation project(':coffeegrinder')
  implementation project(':coffeefilter')
  implementation (
    [group: 'net.sf.saxon', name: 'Saxon-HE', version: saxonVersion],
  )
  documentation (
    [group: saxonGroup, name: saxonEdition, version: saxonVersion],
    [group: 'org.docbook', name: 'schemas-docbook', version: docbookVersion],
    [group: 'org.docbook', name: 'docbook-xslTNG', version: xslTNGversion]
  )
}

testing {
  suites {
    test {
      useJUnitJupiter('5.9.2')
    }
  }
}

java {
  toolchain {
    languageVersion = JavaLanguageVersion.of(8)
  }
}

println("Building with Java version ${System.getProperty('java.version')}")
println("Building with Saxon version ${new Processor(false).getSaxonProductVersion()}")

buildConfig {
  packageName('org.nineml.coffeesacks')
  buildConfigField('String', 'TITLE', "\"${sacksTitle}\"")
  buildConfigField('String', 'VERSION', "\"${ninemlVersion}\"")
}

ext {
  c_coffeesacks = "coffeesacks"
}

docker_container.configure {
  workingDir = "docker"
}

// ============================================================

jar {
  archiveBaseName = "${sacksTitle}-${ninemlVersion}"
  manifest {
    attributes 'Built-By': 'Norman Walsh'
    attributes 'Implementation-Vendor': 'Norman Walsh'
    attributes 'Implementation-Title': sacksTitle
    attributes 'Implementation-Version': ninemlVersion
  }
}

task classlist(
  dependsOn: ["javadoc"]
) {
  inputs.files fileTree(dir: javadoc.destinationDir)
  outputs.file "${buildDir}/classlist.xml"

  def classlist = []
  doLast {
    def out = new PrintStream(new File("${buildDir}/classlist.xml"))
    out.println("<classlist>")
    def plen = "${javadoc.destinationDir}".length() + 1
    def stream = Files.walk(Paths.get("${javadoc.destinationDir}"))
    stream.filter(Files::isRegularFile).forEach(fn -> classlist.add(fn.toString()))
    classlist.forEach { fn ->
      def path = fn.substring(plen)
      if (path.endsWith(".html") && path.startsWith("org/")
          && !path.endsWith("/package-summary.html")
          && !path.endsWith("/package-tree.html")) {
        String className = path.replace("/", ".").replace(".html", "")
        String baseName = className
        int pos = className.lastIndexOf(".")+1
        baseName = className.substring(pos)
        out.println("<class name='${baseName}' fq='${className}' path='${path}'/>")
      }
    }
    out.println("</classlist>")
    out.close()
  }
}

javadoc {
  //inputs.file "${projectDir}/src/main/java/overview.html"
  inputs.files fileTree(dir: "${projectDir}/src/main")

  title 'An Invisible XML Parsing API'
  exclude 'org/nineml/coffeesacks/model/**'
  exclude 'org/nineml/coffeesacks/utils/**'
  exclude 'org/nineml/coffeesacks/BuildConfig.java'
  exclude 'org/nineml/coffeesacks/parser/CommonBuilder.java'
  options.showFromPublic()
  //options.overview = 'src/main/java/overview.html'
  options.encoding('utf-8')
  options.docEncoding('utf-8')
  options.charSet('utf-8')
}

task dist(dependsOn:["test", "jar", "javadoc"]) {
  doLast {
    mkdir "${buildDir}/stage"
    mkdir "${buildDir}/stage/docs"
  }
  doLast {
    copy {
      from "${buildDir}/libs"
      into "${buildDir}/stage"
    }
  }
  doLast {
    copy {
      from "${buildDir}/docs"
      into "${buildDir}/stage/docs"
    }
  }
  doLast {
    copy {
      from "${projectDir}/README.md"
      into "${buildDir}/stage/docs"
    }
  }
}

task zipDist(type: Zip) {
  from "${buildDir}/stage"
  into "${sacksName}-${ninemlVersion}"
  archiveFileName = "${sacksName}-${ninemlVersion}.zip"
}
dist.finalizedBy zipDist

task javadocJar(type: Jar, dependsOn: javadoc) {
  archiveClassifier = 'javadoc'
  from javadoc.destinationDir
}

task sourcesJar(type: Jar, dependsOn: ['generateBuildConfig']) {
  archiveClassifier = 'sources'
  from sourceSets.main.allSource
}

tasks.withType(JavaCompile) {
    options.compilerArgs << '-Xlint:deprecation'
}

// ============================================================

task manual_xinclude(type: SaxonXsltTask, dependsOn: ["copyResources"]) {
  inputs.files fileTree(dir: "${projectDir}/src/website/xml")

  input "${projectDir}/src/website/xml/coffeesacks.xml"
  stylesheet "${projectDir}/../website/xinclude.xsl"
  output "${buildDir}/tmp/coffeesacks.xml"
  args(['-init:org.docbook.xsltng.extensions.Register'])
}

task manual_validate(type: RelaxNGValidateTask, dependsOn: ["manual_xinclude"]) {
  input manual_xinclude.outputs.files.singleFile
  output "${buildDir}/xml/coffeesacks.xml"
  schema "${projectDir}/../website/docbook.rnc"
  compact true
}

task manual_augment(type: SaxonXsltTask, dependsOn: ["manual_validate"]) {
  input manual_validate.outputs.files.singleFile
  output "${buildDir}/aug/coffeesacks.xml"
  stylesheet "${projectDir}/tools/augment.xsl"
  parameters (
    'version': ninemlVersion,
  )
}

task website(type: SaxonXsltTask, dependsOn: ["manual_augment", "classlist"]) {
  inputs.files fileTree(dir: "${projectDir}/tools", include: "*.xsl")
  inputs.files fileTree(dir: "${projectDir}/src/website/xml")
  outputs.files fileTree(dir: "${buildDir}/website")

  input manual_augment.outputs.files.singleFile
  output "${buildDir}/website/index.html"
  stylesheet "${projectDir}/tools/docbook.xsl"
  args(['-init:org.docbook.xsltng.extensions.Register'])
  parameters (
    'relax-ng-grammar': "${projectDir}/../website/docbook.rng",
    'mediaobject-input-base-uri': "file:${buildDir}/aug/",
    'mediaobject-output-base-uri': "/",
    'chunk': 'index.html',
    'chunk-output-base-uri': "${projectDir}/build/website/"
  )
}

task changelog(type: SaxonXsltTask, dependsOn: ["website"]) {
  inputs.file "${projectDir}/../website/docbook.xsl"
  outputs.file "${buildDir}/changelog.txt"

  doFirst {
    Html5Parser.parse("file://${projectDir}/build/website/changelog.html",
                      "${buildDir}/changelog.xhtml")
  }

  input "${buildDir}/changelog.xhtml"
  output "${buildDir}/changelog.txt"
  stylesheet "${projectDir}/../website/changelog.xsl"
  parameters (
    'version': ninemlVersion
  )
}
website.finalizedBy changelog

task publishVersions() {
  doLast {
    StaticPages.versionsJson(currentReleaseVersion, ninemlVersion,
                             "${buildDir}/website/versions.json")
  }
}
website.finalizedBy publishVersions

task publishError404() {
  doLast {
    StaticPages.error404("CoffeeSacks", currentReleaseVersion, "${buildDir}/website/404.html")
  }
}
website.finalizedBy publishError404

task website_javadoc(type: Copy, dependsOn: ["javadoc"]) {
  from javadoc.destinationDir
  into "${buildDir}/website/apidoc"
}
website.finalizedBy website_javadoc

task copyResources(dependsOn: ["copyJarResources", "copyStaticResources"]) {
  // nop
}

task copyJarResources() {
  outputs.files fileTree(dir: "${buildDir}/website")

  def dbjar = null
  configurations.transform.each { path ->
    if (path.toString().contains("docbook-xslTNG")) {
      dbjar = path
    }
  }

  doLast {
    if (dbjar == null) {
      throw new GradleException("Failed to locate DocBook xslTNG jar file")
    }
    copy {
      into "${buildDir}/website"
      from ({ zipTree(dbjar.toString()) }) {
        include "org/docbook/xsltng/resources/**"
      }
      eachFile { fileCopyDetails ->
        def originalPath = fileCopyDetails.path
        fileCopyDetails.path = originalPath.replace('org/docbook/xsltng/resources/', '')
      }
    }
  }

  doLast {
    delete "${buildDir}/website/org"
  }
}

task copyStaticResources() {
  inputs.files fileTree(dir: "${projectDir}/../website/resources")
  inputs.files fileTree(dir: "${projectDir}/src/website/resources")
  inputs.files fileTree(dir: "${projectDir}/src/website/xml")
  outputs.files fileTree(dir: "${buildDir}/website")
  outputs.files fileTree(dir: "${buildDir}/aug")

  doLast {
    copy {
      into "${buildDir}/website"
      from "${projectDir}/src/website/resources"
    }
  }

  doLast {
    copy {
      into "${buildDir}/website"
      from "${projectDir}/../website/resources"
    }
  }

  doLast {
    copy {
      from "${projectDir}/src/website/xml/images"
      into "${buildDir}/website/images"
    }
  }

  // FIXME: Grotesque hack. 
  doLast {
    copy {
      from "${projectDir}/src/website/xml/images"
      into "${buildDir}/aug/images"
    }
  }

  // FIXME: Grotesque hack. 
  doLast {
    copy {
      from "${projectDir}/src/website/resources/images"
      into "${buildDir}/aug/images"
    }
  }

  doLast {
    copy {
      from "${projectDir}/src/website/xml/examples"
      into "${buildDir}/website/examples"
    }
  }
}

// ============================================================

signing {
  sign publishing.publications
}

publishing {
  publications {
    mavenJava(MavenPublication) {
      pom {
        name = sacksTitle
        packaging = 'jar'
        description = 'Saxon extension functions for Invisible XML'
        url = 'https://github.com/nineml/coffeesacks'

        scm {
          url = 'scm:git@github.com:nineml/coffeesacks.git'
          connection = 'scm:git@github.com:nineml/coffeesacks.git'
          developerConnection = 'scm:git@github.com:nineml/coffeesacks.git'
        }

        licenses {
          license {
            name = 'Mozilla Public License Version 2.0'
            url = 'http://www.mozilla.org/MPL/2.0/'
            distribution = 'repo'
          }
        }

        developers {
          developer {
            id = 'ndw'
            name = 'Norman Walsh'
          }
        }
      }

      groupId = "org.nineml"
      artifactId = sacksName
      version = ninemlVersion
      from components.java
      artifact javadocJar
      artifact sourcesJar
    }
  }

  repositories {
    maven {
      url = "https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/"
      credentials {
        username = project.findProperty("sonatypeUsername") ?: "NOUSER"
        password = project.findProperty("sonatypePassword") ?: "NOPASS"
      }
    }
  }
}

// ============================================================

task dockerup() {
  doLast {
    if (!DockerContainer.running(c_coffeesacks)) {
      DockerContainer.compose {
        command = "up"
        options = ["-p", "coffeesacks"]
        args = ["-d"]
      }
    }
  }
}

task dockerdown() {
  doLast {
    if (DockerContainer.running(c_coffeesacks)) {
      DockerContainer.compose {
        command = "down"
        options = ["-p", "coffeesacks"]
      }
    }
  }
}

// ============================================================

task helloWorld() {
  doLast {
    println('Hello, world.')
  }
}
